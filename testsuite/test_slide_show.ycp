/**
 * Module:		test_slide_show.ycp
 *
 * Purpose:		Test environment for slide show 
 *
 * Author:		Stefan Hundhammer <sh@suse.de>
 **/
{
    import "SlideTester";

    UI::OpenDialog(
		   `VBox(
			 `Heading( "Slide Show Tester" ),
			 `VSpacing(),
			 `Frame( "Distribution",
				 `HVSquash(
					   `RadioButtonGroup(`id(`distro),
							     `VBox(
								   `Left(`RadioButton(`id(`suse), "Slide Show for SuSE &Linux", true ) ),
								   `Left(`RadioButton(`id(`sles), "Slide Show for &SLES" ) )
								   )
							     )
					   )
				 ),
			 `VSpacing(),
			 `ComboBox(`id(`lang), `opt(`editable,`hstretch), "&Language", [ "de", "en", "cs" ] ),
			 `VSpacing(),
			 `PushButton(`opt(`default), "&OK" )
			 )
		   );

    UI::UserInput();

    symbol distro = UI::QueryWidget(`id(`distro), `CurrentButton );
    y2milestone( "Distribution: %1", distro );

    if      ( distro == `suse )	SlideTester::SetBasePath( "/SuSE/SuSE/CD1/suse/setup/slide" ); 
    else if ( distro == `sles )	SlideTester::SetBasePath( "/SuSE/SuSE-SLES/CD1/suse/setup/slide" );
    SlideTester::SetLanguage( UI::QueryWidget(`id(`lang), `Value ) );
    
    UI::CloseDialog();

    
    SlideTester::OpenSlideShowDialog();

    symbol button = nil;
    
    repeat
    {
	button = UI::UserInput();

	if ( button == `next )		SlideTester::NextSlide();
	if ( button == `back )		SlideTester::PrevSlide();
	    
    } until ( button == `abort );
    
    UI::CloseDialog();
}
