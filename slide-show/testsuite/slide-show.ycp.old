/**
 * File:	clients/slide-show.ycp
 * Package:	Slide Show
 * Summary:	Test environment for slide show
 * Authors:	Stefan Hundhammer <sh@suse.de>
 *		Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

{

import "Label";
import "SlideTester";

UI::OpenDialog(`VBox(
    `Heading("Slide Show Tester"),
    `VSpacing(),
    `Frame("Distribution", `HVSquash(`RadioButtonGroup(`id(`distro), `VBox(
	`Left(`RadioButton(`id(`suse), "Slide Show for SUSE Linu&x", true ) ),
	`Left(`RadioButton(`id(`sles), "Slide Show for &SLES" ) )
    )))),
    `VSpacing(),
    `ComboBox(`id(`lang), `opt(`editable,`hstretch), "&Language", [ "en", "cs", "de", "es", "fr", "hu", "it", "sk", "bg" ]),
    `VSpacing(),
    `PushButton(`opt(`default), Label::OKButton())
));

UI::UserInput();

symbol distro = (symbol) UI::QueryWidget(`id(`distro), `CurrentButton );
y2milestone("Distribution: %1", distro);

if(distro == `suse) SlideTester::SetBasePath("/SuSE/SuSE/CD1/suse/setup/slide");
else if(distro == `sles) SlideTester::SetBasePath("/SuSE/SuSE-SLES/CD1/suse/setup/slide");

SlideTester::SetLanguage((string) UI::QueryWidget(`id(`lang), `Value));

UI::CloseDialog();

SlideTester::OpenSlideShowDialog();
integer id = (integer) UI::QueryWidget(`id(`imagedata), `WindowID);
y2milestone("id=%1", id);

SCR::Execute(.target.bash_background, sformat("/usr/X11R6/lib/xscreensaver/xflame -window-id %1", id));

symbol button = nil;

repeat {
    button = (symbol) UI::UserInput();
    if(button == `next) SlideTester::NextSlide();
    if(button == `back) SlideTester::PrevSlide();
} until (button == `abort);

UI::CloseDialog();

/* EOF */
}
